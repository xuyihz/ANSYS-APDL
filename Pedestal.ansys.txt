FINISH
/CLEAR
/FILNAME, Pedestal, 1               !定义工作文件名
/TITLE, The Analysis of Pedestal
/REPLOT                             !重新显示

!!!!
!!!!  参数变量
!!!!
*AFUN, DEG  !统一角度单位 (因LOCAL等命令为degree) !*AFUN, Lab!Specifies units for angular functions in parameter expressions.
!常量
PI = ACOS(-1)
Es = 2.06e5         !钢的弹性模量 N/mm2
Gs = 0.79e5         !钢的剪切模量 N/mm2
PRs = Es/(2*Gs)-1   !钢的泊松比
Sfy = 345           !钢的屈服强度 N/mm2 !根据板材厚度查表
!参数
len1 = -600         !支座标高(相对于正负零)
len2 = 1000         !折段长度
len3 = 200          !十字板与圆管重合长度
len4 = 200          !十字板露出长度
lenTB = len1+len4   !圆管底端 TubeBottom
lenCT = lenTB+len3  !十字板顶端 CrossTop
angle1 = 23         !折段角度(与Z轴)
!angle2 = ATAN((len2*cos(angle1))/(len2*sin(angle1)-len1))   !加载点对着支座点的角度 !因len1是负的，故这里为减号
d1 = 250            !管外径
t1 = 10             !管壁厚
t2 = 16             !十字板厚
crossR = d1/2+t1*2  !十字板长度的一半(坐标定位用)
msize = 20          !网格尺寸
!编号
f_n = 1 !加载点节点编号
s_n = 2 !支座点节点编号 
!荷载 N / N*mm
f_x = 63*1000
f_y = 1.5*1000
f_z = -600*1000     !不能有空格，空格后的不计算。
m_x = 2*1000
m_y = 16*1000
m_z = 0
!求解
substeps_num = 10

!!!!
!!!!  前处理
!!!!
/PREP7

!定义局部坐标系
LOCAL, 11, CYLIN    !局部坐标系 柱坐标系
LOCAL, 12, CYLIN, , , , , , angle1  !局部坐标系 斜柱坐标系 (in degrees)
LOCAL, 13, CART, , , , , , angle1   !局部坐标系 斜坐标系
!LOCAL, 14, CART, , , , , , angle2   !局部坐标系 加载点对着支座点的斜坐标系

!激活整体坐标系
CSYS, 0

!!!几何建模
!关键点 !K, NPT, X, Y, Z
K, 1, 0, 0, 0                       !原点
K, 2, 0, 0, len1                    !支座点
K, 3, 0, 0, d1                      !相贯突出点
K, 4, len2*sin(angle1), 0, len2*cos(angle1)     !折段端点
K, 5, -d1*sin(angle1), 0, -d1*cos(angle1)       !折段相贯突出点
K, 6, 0, 0, lenTB                   !圆管底端中心点
K, 7, 0, 0, lenCT                   !十字板顶端中心点
K, 8, -crossR, 0, len1              !十字板左端点
K, 9, crossR, 0, len1               !十字板右端点
K, 10, 0, -crossR, len1             !十字板前端点
K, 11, 0, crossR, len1              !十字板后端点

!线 !L, P1, P2
L, 6, 3     !直管线
L, 4, 5     !斜管线
L, 2, 7     !十字板线
L, 8, 9     !十字板左右线
L, 10, 11   !十字板前后线

!圆 !CIRCLE, PCENT, RAD, PAXIS, PZERO
CIRCLE, 6, d1/2, 3
CSYS, 11                    !激活局部坐标系
LSEL, S, LOC, X, d1/2       !选择刚刚创建的圆的所有线
CM, circle1, LINE           !命名
CIRCLE, 4, d1/2, 5
CSYS, 12                    !激活局部坐标系
LSEL, S, LOC, X, d1/2       !选择刚刚创建的圆的所有线
CM, circle2, LINE           !命名
ALLSEL                      !Selects all entities with a single command.

!拖拉成面 !ADRAG, NL1, NL2, NL3, NL4, NL5, NL6, NLP1, NLP2, NLP3, NLP4, NLP5, NLP6
ADRAG, circle1, , , , , , 1 !拖拉形成主管
ADRAG, circle2, , , , , , 2 !拖拉形成主管
ADRAG, 4, , , , , , 3       !拖拉形成十字板 !一起拖拉有问题，故分两次拖拉。
ADRAG, 5, , , , , , 3       !拖拉形成十字板

!用工作平面切分
CSYS, 0     !激活整体坐标系
WPCSYS, , 0 !工作平面回到整体坐标系
WPROTA, , , angle1/2    !旋转工作平面 !WPROTA, THXY, THYZ, THZX (in degrees)
ASBW, ALL   !ASBW, NA, SEPO, KEEP
CSYS, 0     !激活整体坐标系
ASEL, S, LOC, Z, (len1+lenCT)/2 !选择十字板(几何形心点)
!不进行以下步骤，切分相交处平面，后续执行APTN命令会报错。
!工作平面回到整体坐标系 !旋转工作平面 !WPROTA, THXY, THYZ, THZX !平移工作平面 !WPOFFS, XOFF, YOFF, ZOFF !ASBW, NA, SEPO, KEEP
WPCSYS, , 0 $ WPROTA, , , 90 $ WPOFFS, 0, 0, d1/2 $ ASBW, ALL
WPCSYS, , 0 $ WPROTA, , , 90 $ WPOFFS, 0, 0, -d1/2 $ ASBW, ALL
WPCSYS, , 0 $ WPROTA, , 90,  $ WPOFFS, 0, 0, d1/2 $ ASBW, ALL
WPCSYS, , 0 $ WPROTA, , 90,  $ WPOFFS, 0, 0, -d1/2 $ ASBW, ALL
WPCSYS, , 0
WPSTYL, , , , , , , , 0     !不显示工作平面 !WPSTYL, SNAP, GRSPAC, GRMIN, GRMAX, WPTOL, WPCTYP, GRTYPE, WPVIS, SNAPANG
ALLSEL

CSYS, 11                    !激活局部坐标系
ASEL, S, LOC, X, d1/2       !选择所有直管面
ASEL, R, LOC, Z, 0, d1      !取消选择需要的部分(几何形心在0~d1中的面)
CM, cube1, AREA             !命名
ALLSEL
CSYS, 12                    !激活局部坐标系
ASEL, S, LOC, X, d1/2       !选择所有直管面
ASEL, R, LOC, Z, 0, -d1     !取消选择需要的部分
CM, cube2, AREA             !命名
ALLSEL
ADELE, cube1, , , 1         !删除多余的面
ADELE, cube2, , , 1
CSYS, 11                    !激活局部坐标系
ASEL, S, LOC, Z, (len1+lenCT)/2 !选择十字板(几何形心点)
APTN, ALL                   !Partitions areas. 分隔运算，边界共享。
ALLSEL
APTN, ALL

!!!网格划分
!定义单元类型 TYPE
ET, 1, MASS21   !质量单元
ET, 2, SHELL181 !壳单元
!实常数 REAL
R, 1, 0         !重量
R, 2, t1        !壁厚
R, 3, t2        !壁厚
!材料属性 MAT
MATNUM = 1              !材料参考号
MP, EX, MATNUM, Es      !弹性模量
MP, PRXY, MATNUM, PRs   !泊松比
TB, MISO, MATNUM        !非线性材料 !MISO多线性各向同性强化
TBPT, DEFI, Sfy/Es, Sfy !本构 折线
TBPT, DEFI, 0.5, Sfy

!利用MASS21单元 CERIG命令耦合主节点与从节点群 方便加载与约束
NKPT, f_n, 4            !加载主节点 !在关键点处定义节点 !NKPT, NODE, NPT
NKPT, s_n, 2            !支座主节点
TYPE, 1                 !定义单元类型号
MAT, MATNUM             !定义材料类型号
REAL, 1                 !定义实常数号
E, f_n                  !定义单元
E, s_n                  !定义单元

CSYS, 11                !激活局部坐标系
ASEL, S, LOC, Z, (len1+lenCT)/2 !选择十字板(几何形心点)
AATT, MATNUM, 3, 2      !设置面单元属性!AATT, MAT, REAL, TYPE, ESYS, SECN
ASEL, INVE              !选择除十字板外AREA(即圆管)
AATT, MATNUM, 2, 2      !设置面单元属性!AATT, MAT, REAL, TYPE, ESYS, SECN
ALLSEL                  !全选
LESIZE, circle2, msize*2!边界处可划分的粗一点(加速计算)
ESIZE, msize            !网格尺寸
MSHAPE, 0               !网格形状(0:4/6;1:3/4)
MSHKEY, 0               !网格类型(0自由1映射)
AMESH, ALL              !划分网格

CSYS, 13                !激活局部坐标系
NSEL, S, LOC, Z, len2
NROTAT, ALL             !Rotates nodal coordinate systems into the active system.
!CSYS, 14                !激活局部坐标系(力指向支座)
CSYS, 13                !激活局部坐标系(力指向圆管方向)
NROTAT, f_n             !Rotates nodal coordinate systems into the active system.
CERIG, f_n, ALL, ALL    !加载刚域 !CERIG, MASTE, SLAVE, Ldof, Ldof2, Ldof3, Ldof4, Ldof5 !主节点, 从节点
ALLSEL                  !Selects all entities with a single command.
CSYS, 0                 !激活整体坐标系
NSEL, S, LOC, Z, len1
CERIG, s_n, ALL, ALL    !支座刚域
ALLSEL                  !Selects all entities with a single command.
!NUMMRG, NODE            !此命令会出错，可能把刚域点合并了 !合并重合节点 !NUMMRG, Label, TOLER, GTOLER, Action, Switch 
NUMCMP, NODE            !压缩节点编号

!!!加载
!加约束
CSYS, 0                 !激活整体坐标系
D, s_n, ALL             !刚接 !D, Node, Lab, VALUE, VALUE2, NEND, NINC, Lab2, Lab3, Lab4, Lab5, Lab6
!D, s_n, UX, , , , , UY, UZ  !铰接 !此处铰接会成机构。
!加荷载 !加在节点坐标系上
!F, NODE, Lab, VALUE, VALUE2, NEND, NINC
F, f_n, FX, f_x     !Specifies force loads at nodes.
F, f_n, FY, f_y     !Specifies force loads at nodes.
F, f_n, FZ, f_z     !Specifies force loads at nodes.
F, f_n, MX, m_x     !Specifies force loads at nodes.
F, f_n, MY, m_y     !Specifies force loads at nodes.
F, f_n, MZ, m_z     !Specifies force loads at nodes.

!!!!
!!!!  求解
!!!!
/SOLU

ANTYPE, STATIC  !求解类型
NLGEOM, ON      !大变形
!NSUBST, substeps_num        !Specifies the number of substeps to be taken this load step.
!CUTCONTROL, PLSLIMIT, 0.10  !Controls time-step cutback during a nonlinear solution.
!OUTRES, ALL, 1  !输出选项（1，所有子步）
AUTOTS, ON      !自动时间步
ARCLEN, ON      !激活弧长法(可算下降段)
!ARCTRM, U, 18, , UY !Controls termination of the solution when the arc-length method is used.
!NROPT, FULL     !Specifies the Newton-Raphson options in a static or full transient analysis.
!ERESX, NO       !Specifies extrapolation of integration point results.
SSTIF, ON       !应力刚化
SOLVE
FINISH

!!!!
!!!!  后处理
!!!!
/POST1      !通用后处理
CSYS, 0                 !激活整体坐标系
/VIEW, 1, 0, -1, 0      !选择视图 /VIEW, WN, XV, YV, ZV !视角为从(0,0,0)往(XV,YV,ZV)看
!PLNSOL, Item, Comp, KUND, Fact, FileID, AVG
PLNSOL, S, EQV  !显示节点MISES应力 !Displays solution results as continuous element contours.
